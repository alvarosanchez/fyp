import org.apache.pdfbox.util.PDFMergerUtility

buildscript {
	dependencies {
		classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.9'
        classpath 'org.apache.pdfbox:pdfbox:1.8.10'
	}
}
plugins {
  id 'org.ajoberstar.github-pages' version '1.1.0'
  id 'org.asciidoctor.gradle.asciidoctor' version '1.5.1'
  id 'org.asciidoctor.convert' version '1.5.2'
}

githubPages {
  repoUri = 'https://github.com/alvarosanchez/fyp.git' // Set this to the destination repo

  credentials {
    username = project.hasProperty('githubToken') ? project.githubToken : ''
    password = ''
  }

  pages {
    from file(asciidoctor.outputDir.path + '/html5')
    from file(asciidoctor.outputDir.path + '/pdf')
  }
}

version = '1.0.0-SNAPSHOT'

task prependCoverPage << {
    PDFMergerUtility merger = new PDFMergerUtility()
    merger.addSource(file('src/docs/themes/cover.pdf'))
    merger.addSource(file(asciidoctor.outputDir.path + '/pdf/index.pdf'))
    merger.setDestinationFileName('out.pdf')
    merger.mergeDocuments()

    file('out.pdf').renameTo file(asciidoctor.outputDir.path + '/pdf/index.pdf')
}

asciidoctor {
    backends    'html5','pdf'
    attributes  'pdf-stylesdir' : '../themes',
                'pdf-fontsdir'  : '../fonts',
                'pdf-style'     : 'main',
                'doctype'       : 'book',
                'experimental'  : 'true',
                'compat-mode'   : 'true',
                'toc'           : 'true',
                'icons'         : 'font'
}

prependCoverPage.dependsOn asciidoctor
publishGhPages.dependsOn prependCoverPage
